#############################################
## Realtime visualization
## Example call:
##    gnuplot -e "num_bodies=1000;steps=1000;stride=10" visualizeData.plg

set term x11;
set nokey
set autoscale

# Number of bodies in file
if (!exists("num_bodies")) num_bodies = 10000

# Input data file name
if (!exists("in_filename")) in_filename = sprintf('out%d.out',num_bodies)

# Number of steps in file
if (!exists("steps")) steps = 1000

# Skip every stride steps
if (!exists("stride")) stride = 1

print "In File name: ", in_filename

do for [i = 0:steps-1:stride] {
	print sprintf("On #%d/%d",i,steps);
	splot in_filename using 1:2:3 every ::2+(i*num_bodies)::2+((i+1)*num_bodies) with points ls '0';
	#pause 0.01
}
